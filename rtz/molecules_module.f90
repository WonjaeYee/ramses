! molecules_module.f90
module molecules_module
  use amr_parameters, only: dp
  use safe_math, only: safe_exp
  implicit none

  private  ! everything is private by default
  public :: alpha_H2, beta_H2_umist, beta_H2, beta_H2_krome, alpha_CO, beta_CO, alpha_H2_prim, alpha_H2_dust
  public :: comp_SH2, comp_Sd, comp_SCO, initialize_SCO_table

  real(dp), dimension(52, 2):: sco_table ! self shielding coefficient vs. CO column density
  real(dp), dimension(43, 2):: sh2_table ! self shielding coefficient vs. H2 column density

CONTAINS

FUNCTION alpha_H2_prim(T, xe, H2_cosmic_ray_ionization_rate, G0, xHI, xHII, nH) result(rate)
  implicit none

  real(dp), intent(in) :: T, xe, H2_cosmic_ray_ionization_rate, G0
  real(dp), intent(in) :: xHI, xHII, nH
  real(dp) :: rate
  real(dp) :: logT, lnTe, logT2, Te
  real(dp) :: k1, k2, k5, k13, k14, k15, k_hm_cr, k_hm_gamma
  real(dp) :: cr_photo_destruction, collisional_destruction

  ! H- channel for H2 formation
  rate = 0.d0

  ! Primordial channel
  logT = log10(T)
  Te = T*8.621738d-5 ! K -> eV
  lnTe = log(Te)

  ! Creation and destruction channels of H- included with updated rates from Glover et al. 2010
  ! H + e- -> H- + gamma
  if (T .lt. 6000.d0) then
     k1 = 10.d0**(-17.845d0 + logT * (0.762d0 + logT * (0.1523d0 - 0.03274d0 * logT)))
  else
     logT2 = logT * logT
     k1 = 10.d0**(-16.42d0 + logT2 * (0.1998d0 + logT2 * (-5.447d-3 + 4.0415d-5 * logT2)))
  end if

  ! H- + H -> H2 + e
  k2 = 4.0d-9*(max(T,300.d0)**(-0.17d0))

  ! H- + H+ -> H + H
  k5 = 2.4d-6/sqrt(T)*(1.d0 + T/20000.d0)

  ! H- + CR --> H + e-
  k_hm_cr = 1.28d-13 * (H2_cosmic_ray_ionization_rate / 1.d-16)

  ! H- + gamma --> H + e-
  k_hm_gamma = 5.9d-9 * G0

  ! H- + e -> H + e + e
  k13 = -1.801849334d1 + lnTe * (2.36085220d0 + lnTe * (-2.82744300d-1 + lnTe * (1.62331664d-2 + lnTe * (-3.36501203d-2 + lnTe * (1.17832978d-2 + lnTe * (-1.65619470d-3 + lnTe * (1.06827520d-4 - 2.63128581d-6 * lnTe)))))))
  k13 = safe_exp(max(k13,-92.d0)) ! Max needed to prevent result from diverging at low temperature

  ! H- + H --> H + H + e-
  ! I think this reaction was broken in glover so I took the results from
  ! https://www.aanda.org/articles/aa/pdf/2016/02/aa27262-15.pdf Table A1
  ! Harley added the fudge factor for continuity
  k14 = 1.357772745525155d0 * 2.5634d-15 * (Te**1.78186d0) ! Note that T must be in eV for this reaction to make sense
  if (T .gt. 1160.d0) then
     k14 = -3.388464953d1 + lnTe * (1.13944933d0 + lnTe * (-1.4210135d-1 + lnTe * (8.4644554d-3 + lnTe * (-1.4328641d-3 + lnTe * (2.0122503d-4 + lnTe * (8.6639632d-5 + lnTe * (-2.5850097d-5 + lnTe * (2.4555012d-6 - 8.0683825d-8 * lnTe))))))))
     k14 = safe_exp(k14)
  end if

  ! H- + H+ --> H2+ + e- --> H + H (via recombinative dissociation)
  k15 = 6.9d-9 * (T**(-0.35d0))
  if (T .gt. 8000.d0) then
     k15 = 9.6d-7 * (T**(-0.9d0))
  end if

  ! Correct steady state denominator terms scaled to cm^3 s^-1
  cr_photo_destruction = (k_hm_cr + k_hm_gamma) / (nH + 1d-40)
  collisional_destruction = k2 * xHI + k5 * xHII + k13 * xe + k14 * xHI + k15 * xHII

  rate = rate + k1 * k2 * xe * xHI / (collisional_destruction + cr_photo_destruction)

  rate = MAX(rate,1.d-100)

END FUNCTION alpha_H2_prim

FUNCTION alpha_H2_dust(T, dust_to_gas_mass_ratio_over_mw) result(rate)
  ! Formation on dust
  use rt_parameters, only: rtz_H2_clumping
  implicit none

  real(dp), intent(in) :: T, dust_to_gas_mass_ratio_over_mw
  real(dp) :: rate
  real(dp) :: T2

  T2 = T / 100.d0
  rate = dust_to_gas_mass_ratio_over_mw * (3.5d-17) * rtz_H2_clumping * sqrt(min(T2,1.d2))

  rate = MAX(rate,1.d-100)

END FUNCTION alpha_H2_dust

FUNCTION alpha_H2(T, dust_to_gas_mass_ratio_over_mw, xe, H2_cosmic_ray_ionization_rate, G0, xHI, xHII, nH) result(rate)
  ! Creation rate of molecular hydrogen
  ! We consider both the primordial channel (via H-) as well
  ! as formation on dust
  implicit none
  real(dp), intent(in) :: T, dust_to_gas_mass_ratio_over_mw, xe
  real(dp), intent(in) :: H2_cosmic_ray_ionization_rate, G0, xHI
  real(dp), intent(in) :: xHII, nH
  real(dp) :: rate

  rate = 0.d0

  ! Formation rate on dust. Consider only HI
  if (dust_to_gas_mass_ratio_over_mw.gt.0d0) then
     rate = rate + alpha_H2_dust(T, dust_to_gas_mass_ratio_over_mw) * xHI * nH
  end if

  ! Primordial H- channel
  rate = rate + alpha_H2_prim(T, xe, H2_cosmic_ray_ionization_rate, G0, xHI, xHII, nH) * xHI * nH

  rate = MAX(rate,1.d-100)

END FUNCTION alpha_H2

FUNCTION beta_H2_umist(T, nH, ne, nH2) result(rate)
  ! H2 destruction from umist
  implicit none

  real(dp), intent(in) :: T, nH, ne, nH2
  real(dp) :: rate
  real(dp) :: T_loc

  rate = 0.d0

  !H2 + H2 --> H2 + H + H 
  T_loc = max(min(T,41000.d0),2803.d0)
  rate = rate + (1.00d-8 * ((T_loc/300d0)**0.0d0) * safe_exp(-84100.d0/T_loc) * nH2)

  !H2 + e- --> H + H + e-
  T_loc = max(min(T,41000.d0),3400.d0)
  rate = rate + (3.22d-9 * ((T_loc/300d0)**0.35d0) * safe_exp(-102000.d0/T_loc) * ne)

  !H2 + H --> H + H + H
  T_loc = max(min(T,41000.d0),1833.d0)
  rate = rate + (4.67d-7 * ((T_loc/300d0)**(-1.d0)) * safe_exp(-55000.d0/T_loc) * nH)

  rate = MAX(rate,1.d-100)

END FUNCTION beta_H2_umist

FUNCTION beta_H2_krome(T, nH, ne, nH2, nHe) result(rate)
  ! From bovino 2016
  ! https://www.aanda.org/articles/aa/pdf/2016/06/aa28158-16.pdf
  implicit none
  real(dp), intent(in) :: T, nH, ne, nH2, nHe
  real(dp) :: rate

  rate = 0.d0

  ! H2 + H --> H + H + H (k18)
  rate = rate + ((6.67d-12 * sqrt(T) * safe_exp(-1.d0 * (1.d0 + (63593.d0/T)))) * nH)

  ! H2 + H2 --> H2 + H + H (k19)
  rate = rate + ((5.996d-30 * (T**4.1881d0) * ((1.d0 + (6.761d-6 * T))**(-5.6881d0)) * safe_exp(-54657.4d0/T)) * nH2)

  ! H2 + e- --> H + H + e- (k22)
  rate = rate + (4.38d-10 * (T**0.35d0) * safe_exp(-102000.d0/T) * ne)

  ! H2 + He --> H + H + He
  rate = rate + ((10.d0**(-27.029d0 + (3.801d0 * log10(T)) - (29487.d0/T))) * nHe)

  rate = MAX(rate,1.d-100)

END FUNCTION beta_H2_krome

FUNCTION beta_H2(T, nH, xHI, xH2, xHe, ne, nHI, nH2, nHeI) result(rate)
  ! Returns the collisional dissociation rates of H2 for four different
  ! reactions [cm3s-1] from Glover & Abel (2008)
  ! http://mnras.oxfordjournals.org/content/388/4/1627.full.pdf 
  implicit none

  real(dp), intent(in):: T, nH, xHI, xH2, xHe
  real(dp), intent(in):: ne, nHI, nH2, nHeI
  real(dp):: rate
  real(dp):: T4, ncrH, ncrH2, ncrHe, invncr
  real(dp):: LTEfac, NLTEfac
  real(dp):: k8, k9, k9L, k10, k10L, k11, k11L
  real(dp):: lk8, lk9, lk10, lk11

  T4 = T / 1d4

  ! Critical number densities.  See eqns 15, 16, and 17
  ncrH =  10.d0**(3.d0 - 0.416d0*log10(T4) - 0.327d0*log10(T4)*log10(T4))
  ncrH2 = 10.d0**(4.845d0 - 1.3d0*log10(T4) + 1.62d0*log10(T4)*log10(T4))
  ncrHe = 10.d0**(5.0792d0*(1.d0 - 1.23d-5*(T - 2000.d0)))

  ! 1/ncr.  see eqn 14
  invncr = (xHI/ncrH) + (xH2/ncrH2) + (xHe/ncrHe)

  ! prefactors for LTE and NLTE collision rates  see eqn 13
  LTEfac = (nH*invncr)/(1.d0 + (nH*invncr))
  NLTEfac = 1.d0/(1.d0 + (nH*invncr))
  if (ne .gt. nHI) then
     LTEfac = 1.d0
     NLTEfac = 0.d0
  end if

  !reaction rates from the appendix:
  !H2 + e- --> H + H + e-
  k8 = 3.73d-9 * (T**0.1121d0) * safe_exp(-99430.d0/T) ! Glover et al. (2010)

  !H2 + H --> H + H + H
  k9 = (6.67d-12)*sqrt(T)*safe_exp(-1.d0*(1.d0 + (63593.d0/T)))
  k9L = (3.52d-9)*safe_exp(-43900.d0/T)

  !H2 + H2 --> H2 + H + H
  k10 = ( (5.996d-30*(T**4.1881d0)) / ((1.d0 + 6.761d-6*T)**5.6881d0)) * safe_exp(-54657.4d0/T)
  k10L = (1.3d-9)*safe_exp(-53300.d0/T)

  !H2 + He --> H + H + He
  k11 = 10.d0**(-27.029d0 + (3.801d0*log10(T)) - (29487.d0/T))
  k11L = 10.d0**(-2.729d0 - (1.75d0*log10(T)) - (23474.d0/T))

  k8   = max(k8, 1d-40)
  k9   = max(k9, 1d-40)
  k10  = max(k10, 1d-40)
  k11  = max(k11, 1d-40)
  k9L  = max(k9L, 1d-40)
  k10L = max(k10L, 1d-40)
  k11L = max(k11L, 1d-40)

  !Log of all the rates
  lk8 = log10(k8)
  lk9 = (LTEfac*log10(k9L)) + (NLTEfac*log10(k9))
  lk10 = (LTEfac*log10(k10L)) + (NLTEfac*log10(k10))
  lk11 = (LTEfac*log10(k11L)) + (NLTEfac*log10(k11))

  rate = (ne*(10.d0**lk8)) + (nHI*(10.d0**lk9)) + (nH2*(10.d0**lk10)) + (nHeI*(10.0**lk11))
  rate = max(rate, 1d-40)

  rate = MAX(rate,1.d-100)

END FUNCTION beta_H2

FUNCTION alpha_CO(G0, xi_cr_H2, nCII, nH2, xO, nH) result(rate)
  ! see glover 2012
  implicit none

  real(dp), intent(in):: G0, xi_cr_H2, nCII, nH2, xO, nH
  real(dp):: rate
  real(dp):: k0, k1, gammaCHx_cr, gammaCHx, beta

  k0 = 5.d-16 ! cm^3 s^-1
  k1 = 5.d-10 ! Rate coefficient for the formation of CO from O + CHx

  ! Assuming CO formation is modulated by CH2+, https://home.strw.leidenuniv.nl/~ewine/photo/display_ch2+_65e31a07e69e64dbd64d37801983018f.html
  gammaCHx_cr = 8.88d-15 * (xi_cr_H2 / 1d-16) ! Cosmic rays
  gammaCHx = (1.41d-10 * G0) + gammaCHx_cr

  beta = k1 * xO/(k1*xO + gammaCHx/(nH + 1d-40))
  rate = k0 * nCII * nH2 * beta

  rate = MAX(rate,1.d-100)

END FUNCTION alpha_CO

FUNCTION beta_CO(G0, xi_cr_H2) result(rate)
  ! CO destruction
  implicit none

  real(dp), intent(in):: G0, xi_cr_H2
  real(dp):: rate
  real(dp):: gammaCO, gammaCO_cr

  gammaCO = 2.43d-10 * G0
  gammaCO_cr = 4.62d-15 * (xi_cr_H2 / 1d-16)

  rate = gammaCO + gammaCO_cr

  rate = MAX(rate,1.d-100)

END FUNCTION beta_CO

FUNCTION comp_Sd(tau_dust) result(ss_factor)
  ! Returns the dust LW shielding factor given a pre-computed optical depth.
  ! The caller is responsible for computing tau_dust from either the per-bin
  ! CALIMA grain opacities (recommended with #CALIMA) or the fixed MW
  ! cross-section formula:  tau = 2.34e-21 * Z * (N_HI + 2*N_H2).
  implicit none
  real(dp), intent(in) :: tau_dust
  real(dp)             :: ss_factor
  ss_factor = safe_exp(-tau_dust)
END FUNCTION comp_Sd

FUNCTION comp_SH2(nH2, dx_SS) result(ss_factor)
  ! Returns the self shielding factor for dust
  ! see section 2.2 http://iopscience.iop.org/0004-637X/697/1/55/pdf/apj_697_1_55.pdf
  implicit none
  real(dp), intent(in):: nH2, dx_SS
  real(dp):: ss_factor
  real(dp):: xfac, cNH2, wH2, Sa, Sb, Sc

  cNH2 = nH2*dx_SS  !H2 column density
  xfac = cNH2/(5.d14)
  wH2 = 0.2d0

  Sa = (1.d0 - wH2)/((1.d0 + xfac)*(1.d0 + xfac))
  Sb = wH2/sqrt(1.d0 + xfac)
  Sc = safe_exp(-0.00085d0*sqrt(1.d0 + xfac))

  ss_factor = Sa + (Sb*Sc)
END FUNCTION comp_SH2

SUBROUTINE initialize_SCO_table()
  implicit none

  sco_table(:,1) = (/ &
  1.000d+00, 1.000d+12, 1.650d+12, 2.995d+12, 5.979d+12, 1.313d+13, &
  3.172d+13, 8.429d+13, 2.464d+14, 7.923d+14, 1.670d+15, 2.595d+15, &
  4.435d+15, 6.008d+15, 8.952d+15, 1.334d+16, 1.661d+16, 2.274d+16, &
  3.115d+16, 4.266d+16, 5.843d+16, 8.002d+16, 1.096d+17, 1.501d+17, &
  2.055d+17, 2.815d+17, 4.241d+17, 6.389d+17, 9.625d+17, 1.450d+18, &
  2.184d+18, 3.291d+18, 4.124d+18, 5.685d+18, 7.838d+18, 1.080d+19, &
  1.285d+19, 1.681d+19, 2.199d+19, 2.538d+19, 3.222d+19, 4.091d+19, &
  5.193d+19, 5.893d+19, 7.356d+19, 8.269d+19, 9.246d+19, 1.031d+20, &
  1.148d+20, 1.277d+20, 1.419d+20, 1.578d+20 /)

  sco_table(:,2) = (/ &
  1.000d+00, 9.990d-01, 9.981d-01, 9.961d-01, 9.912d-01, 9.815d-01, &
  9.601d-01, 9.113d-01, 8.094d-01, 6.284d-01, 4.808d-01, 3.889d-01, &
  2.827d-01, 2.293d-01, 1.695d-01, 1.224d-01, 1.017d-01, 7.764d-02, &
  5.931d-02, 4.546d-02, 3.506d-02, 2.728d-02, 2.143d-02, 1.700d-02, &
  1.360d-02, 1.094d-02, 8.273d-03, 6.283d-03, 4.773d-03, 3.611d-03, &
  2.704d-03, 1.986d-03, 1.657d-03, 1.258d-03, 9.332d-04, 6.745d-04, &
  5.596d-04, 4.123d-04, 2.982d-04, 2.490d-04, 1.827d-04, 1.324d-04, &
  9.473d-05, 7.891d-05, 5.668d-05, 4.732d-05, 3.967d-05, 3.327d-05, &
  2.788d-05, 2.331d-05, 1.944d-05, 1.619d-05 /)

  sh2_table(:,1) = (/ &
  1.000d+00, 2.666d+13, 3.801d+14, 6.634d+15, 8.829d+16, 9.268d+17, &
  1.007d+18, 2.021d+18, 3.036d+18, 4.051d+18, 5.066d+18, 6.082d+18, &
  7.097d+18, 8.112d+18, 9.341d+18, 1.014d+19, 2.030d+19, 3.045d+19, &
  4.061d+19, 5.076d+19, 6.092d+19, 7.107d+19, 8.123d+19, 9.353d+19, &
  1.015d+20, 2.031d+20, 3.047d+20, 4.062d+20, 5.078d+20, 6.094d+20, &
  7.109d+20, 8.125d+20, 9.355d+20, 1.016d+21, 2.031d+21, 3.047d+21, &
  4.063d+21, 5.078d+21, 6.094d+21, 7.110d+21, 8.125d+21, 9.355d+21, &
  1.016d+22 /) 

  sh2_table(:,2) = (/ &
  1.000d+00, 9.999d-01, 9.893d-01, 9.678d-01, 9.465d-01, 9.137d-01, &
  9.121d-01, 8.966d-01, 8.862d-01, 8.781d-01, 8.716d-01, 8.660d-01, &
  8.612d-01, 8.569d-01, 8.524d-01, 8.497d-01, 8.262d-01, 8.118d-01, &
  8.011d-01, 7.921d-01, 7.841d-01, 7.769d-01, 7.702d-01, 7.626d-01, &
  7.579d-01, 7.094d-01, 6.712d-01, 6.378d-01, 6.074d-01, 5.791d-01, &
  5.524d-01, 5.271d-01, 4.977d-01, 4.793d-01, 2.837d-01, 1.526d-01, &
  7.774d-02, 3.952d-02, 2.093d-02, 1.199d-02, 7.666d-03, 5.333d-03, &
  4.666d-03 /) 

END SUBROUTINE initialize_SCO_table

FUNCTION comp_SCO(nco_mol, nh2, dx_SS) result(ss_factor)
  ! Returns the self shielding factor for CO
  implicit none
  real(dp), intent(in):: nco_mol, nh2, dx_SS
  real(dp):: ss_factor
  real(dp):: logsCO, effcNCO, logeffcNCO
  real(dp):: logsH2, effcNH2, logeffcNH2
  integer:: i, idxCO, idxH2 ! Lower closest index

  ! initialize to 1.0
  ss_factor = 1.d0

  ! Pinned to table at lower boundary, extrapolated above upper boundary
  effcNCO = MAX(nco_mol*dx_SS, sco_table(1,1))
  if (effcNCO.ge.1.578d+20) then 
     ss_factor = ss_factor * 1.619d-05
  else
     ! Find the lower closest index
     idxCO = 1
     do i=1, 51 ! If above upper boundary then extrapolate using the slope 
        !between the last two points, so conveniently we set i to not go to 52
        if (effcNCO .ge. sco_table(i,1)) idxCO = i
     end do

     logeffcNCO = log10(effcNCO)
     ! Interpolate or extrapolate automatically
     logsCO = log10(sco_table(idxCO,2)) &
     + (log10(sco_table(idxCO+1,2)) - log10(sco_table(idxCO,2))) &
     * (logeffcNCO - log10(sco_table(idxCO,1))) &
     / (log10(sco_table(idxCO+1,1)) - log10(sco_table(idxCO,1)))

     ss_factor = ss_factor * (10.d0 ** logsCO)
  end if


  ! Pinned to table at lower boundary, extrapolated above upper boundary
  effcNH2 = MAX(nh2*dx_SS, sh2_table(1,1))
  if (effcNH2.ge.1.016d+22) then 
      ss_factor = ss_factor * 4.666d-03
  else 
      ! Find the lower closest index
      idxH2 = 1
      do i=1, 42 ! If above upper boundary then extrapolate using the slope 
        !between the last two points, so conveniently we set i to not go to 43
        if (effcNH2 .ge. sh2_table(i,1)) idxH2 = i
      end do

      logeffcNH2 = log10(effcNH2)
      ! Interpolate or extrapolate automatically
      logsH2 = log10(sh2_table(idxH2,2)) &
      + (log10(sh2_table(idxH2+1,2)) - log10(sh2_table(idxH2,2))) &
      * (logeffcNH2 - log10(sh2_table(idxH2,1))) &
      / (log10(sh2_table(idxH2+1,1)) - log10(sh2_table(idxH2,1)))

      ss_factor = ss_factor * (10.d0 ** logsH2)
  end if

END FUNCTION comp_SCO


end module molecules_module
